      SUBROUTINE TTBBWW(I1,I2,WT)
* THE MATRIX ELEMENT FOR THE PROCESS
*    QBAR(I1) Q(I2) ---> T TBAR
* FOLLOWED BY
*    TBAR ---> BBAR(3) E-(4) NUEBAR(5)
*    T    ---> B(6)    NUE(7)  E+(8)
*
*  NB This notation (which is correct) does not correspond to 
*  Eqs. (3,4) in Z. Phys C40 419, 1988. The latter is therefore wrong.
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DOUBLE COMPLEX RPL,RMN,DEN,SPROD
      DOUBLE COMPLEX SPL(10,10),SMN(10,10)
      COMMON/COUPS/GW,GS
      COMMON/CSTD/SPL,SMN
      COMMON/MOM/PLAB(4,10)
      COMMON/PARS/RMT,RGT,RMW,RGW,RMB,RMTLO,RMTUP
      DATA INIT/0/
      SAVE RMW2,RMGW,RMT2,RMB2,RMGT,SCALE,INIT
      IF(INIT.NE.0) GOTO 1
      INIT=1
      SCALE=1D1
      RMW2=RMW**2/SCALE
      RMGW=RMW*RGW/SCALE
      RMT2=RMT**2/SCALE
      RMB2=RMB**2/SCALE
      RMGT=RMT*RGT/SCALE
  1   CONTINUE
*
* THE DENOMINATOR

      D12=DOTKS(1,2)/SCALE
      D34=DOTKS(3,4)/SCALE
      D35=DOTKS(3,5)/SCALE
      D45=DOTKS(4,5)/SCALE
      D67=DOTKS(6,7)/SCALE
      D68=DOTKS(6,8)/SCALE
      D78=DOTKS(7,8)/SCALE
      XT=   2.D0*(D67+D68+D78) + RMB2
      XTBAR=2.D0*(D34+D35+D45) + RMB2
      DEN=2.D0*D12*
     . DCMPLX(XT   -RMT2,RMGT)*
     . DCMPLX(XTBAR-RMT2,RMGT)*
     . DCMPLX(2.D0*D45-RMW2,RMGW)*
     . DCMPLX(2.D0*D78-RMW2,RMGW)
*
* THE AUXILIARY VECTORS FROM THE T AND TBAR MOMENTA
      CT   =XT   /(2.D0*(D68+D78))
      CTBAR=XTBAR/(2.D0*(D34+D45))
      DO 11 K=1,4
      PLAB(K,9) =PLAB(K,3)+(1.D0-CTBAR)*PLAB(K,4)+PLAB(K,5)
      PLAB(K,10)=PLAB(K,6)+PLAB(K,7)+(1.D0-CT   )*PLAB(K,8)
   11 CONTINUE
*
      CALL STD
      
* THE SPINOR PART OF THE NUMERATOR
      SPROD=-SPL(8,10)*SMN(9,4)/DCMPLX(SCALE)
      RPL=(SPROD*SMN(10,I2)*SPL(I1,9)+RMT2*SPL(8,I1)*SMN(I2,4))/SCALE
      RMN=(SPROD*SMN(10,I1)*SPL(I2,9)+RMT2*SPL(8,I2)*SMN(I1,4))/SCALE

*
* THE WHOLE THING
      WT=GW**8*(GS/SCALE)**4*
     . D67*D35*128.D0**2*
     . (ABS(RPL)**2 + ABS(RMN)**2)/ABS(DEN)**2
* ADD B,BBAR SPIN SUM, Q,QBAR AVERAGE, COLOUR SUM &AVERAGE
      WT=WT *4.D0         /4.D0        *2.D0       /9.D0
*
      RETURN
      END
C
