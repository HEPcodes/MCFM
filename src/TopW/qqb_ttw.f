      subroutine qqb_ttw(pin,msq)
      implicit none
C***********************************************************************
C     Author: R.K. Ellis                                               *
C     May, 2011.                                                       *
C     calculate the Born matrix element squared                        *
C     for the process                                                  *
c     My notation                                                      *
*     q(-p1) +qbar(-p2) -> t(nu(p3)+e+(p4)+b(p5))                      *
*                         +tbar(bbar(p6)+e-(p7)+nubar(p8))             *
*                         +W(l(p9)+a(p10))                             *
C                                                                      *
C w=9+10                                                               *
C q=3+4+5                                                              *
C a=-6-7-8                                                             *
C since momenta 3,5,6,8 are not needed they are reused to              *
C represent the de-massified vectors.                                  *
C Thus q4 = q de-massified wrt p4 etc;                                 *
C q4(mu)=q(mu)-qsq/2/p4Dq*p4(mu)-->p(3,mu)                             *
C a7(mu)=a(mu)-asq/2/p7Da*p7(mu)-->p(5,mu)                             *
C Formula generated by the program qqbttbw.frm                         *
************************************************************************
      include 'constants.f'
      include 'ewcouple.f'
      include 'ckm.f'
      include 'zprods_com.f'
      include 'qcdcouple.f'
      include 'masses.f'
      
      integer j,k,nu,q4,a7,p1,p2,p4,p7,p9,p10
      parameter(p1=1,p2=2,q4=3,p4=4,a7=5,p9=6,p7=7,p10=8)
      double precision msq(-nf:nf,-nf:nf),pin(mxpart,4),p(mxpart,4),
     & pw1(4),pw2(4),q(4),a(4),w(4),w1(4),w2(4),p12w(4),
     & sw,sw1,sw2,qDq,aDa,densq,p3Dp5,p6Dp8,
     & wtqqb,wtqbq,s12w,mt2,gamq4,gama7,dot,
     & facqqb,p4Dq,p7Da,denwp1,denwp2
      double complex mqqb,mqbq
c      write(6,*) 'mt',mt,twidth,wmass,wwidth,wmass,wwidth
c      write(6,*) 'Enter scalefac**2'
c      read(5,*)  scalefac
c      scalefac=sqrt(scalefac)
c      mt=scalefac*mt
c      twidth=scalefac*twidth
c      wmass=scalefac*wmass
c      wwidth=scalefac*wwidth
c      wmass=scalefac*wmass
c      wwidth=scalefac*wwidth
      
      mt2=mt**2
      do nu=1,4
c      do j=1,mxpart
c      p(j,nu)=scalefac*pin(j,nu)
c      p(j,nu)=pin(j,nu)
c      enddo
      w(nu)=pin(9,nu)+pin(10,nu)
      p12w(nu)=pin(1,nu)+pin(2,nu)+w(nu)
      pw1(nu)=pin(3,nu)+pin(4,nu)
      pw2(nu)=pin(7,nu)+pin(8,nu)
      q(nu)=+pin(3,nu)+pin(4,nu)+pin(5,nu)
      a(nu)=-pin(6,nu)-pin(7,nu)-pin(8,nu)
      w1(nu)=+w(nu)+pin(1,nu)
      w2(nu)=-w(nu)-pin(2,nu)
      enddo
      sw=(w(4)**2-w(1)**2-w(2)**2-w(3)**2)
      sw1=(pw1(4)**2-pw1(1)**2-pw1(2)**2-pw1(3)**2)
      sw2=(pw2(4)**2-pw2(1)**2-pw2(2)**2-pw2(3)**2)
      s12w=(p12w(4)**2-p12w(1)**2-p12w(2)**2-p12w(3)**2)
      qDq=(q(4)**2-q(1)**2-q(2)**2-q(3)**2)
      aDa=(a(4)**2-a(1)**2-a(2)**2-a(3)**2)
      p3Dp5=dot(pin,3,5)
      p6Dp8=dot(pin,6,8)
      densq=((qDq-mt**2)**2+mt**2*twidth**2)
     &     *((aDa-mt**2)**2+mt**2*twidth**2)
     &     *((sw1-wmass**2)**2+wmass**2*wwidth**2)
     &     *((sw2-wmass**2)**2+wmass**2*wwidth**2)
     &     *((sw-wmass**2)**2+wmass**2*wwidth**2)*s12w**2
      denwp1=w1(4)**2-w1(1)**2-w1(2)**2-w1(3)**2
      denwp2=w2(4)**2-w2(1)**2-w2(2)**2-w2(3)**2
      facqqb=V/4d0*(4d0*p3Dp5*p6Dp8)*4d0*gsq**2*gwsq**6/densq
      p4Dq=pin(4,4)*q(4)-pin(4,1)*q(1)-pin(4,2)*q(2)-pin(4,3)*q(3)
      p7Da=pin(7,4)*a(4)-pin(7,1)*a(1)-pin(7,2)*a(2)-pin(7,3)*a(3)
      gamq4=qDq/(2d0*p4Dq)
      gama7=aDa/(2d0*p7Da)
C     now the momenta 3,5,6,8 are no longer needed, so set
C      parameter(p1=1,p2=2,q4=3,p4=4,a7=5,p9=6,p7=7,p10=8)
      do nu=1,4
      p(p1,nu)=pin(1,nu)
      p(p2,nu)=pin(2,nu)
      p(q4,nu)=q(nu)-gamq4*pin(4,nu)
      p(p4,nu)=pin(4,nu)
      p(a7,nu)=a(nu)-gama7*pin(7,nu)
      p(p7,nu)=pin(7,nu)
      p(p9,nu)=pin(9,nu)
      p(p10,nu)=pin(10,nu)
      enddo
      mqbq=czip
      mqqb=czip
      
      call spinoru(8,p,za,zb)
c      mqbq=mqbq + denwp1**(-1) * ( za(p1,p9)*za(p1,q4)*za(p7,a7)*zb(p1,
c     &    p10)*zb(p2,a7)*zb(p4,q4) + za(p1,p9)*za(p7,a7)*za(p9,q4)*zb(
c     &    p2,a7)*zb(p4,q4)*zb(p9,p10) )
c      mqbq = mqbq + denwp2**(-1) * (  - za(p1,q4)*za(p2,p9)*za(p7,a7)*
c     &    zb(p2,p10)*zb(p2,a7)*zb(p4,q4) + za(p1,q4)*za(p7,a7)*za(p9,
c     &    p10)*zb(p2,p10)*zb(p4,q4)*zb(p10,a7) )
c      mqbq = mqbq + mt2*denwp1**(-1) * (  - za(p1,p7)*za(p1,p9)*zb(p1,
c     &    p10)*zb(p2,p4) + za(p1,p9)*za(p7,p9)*zb(p2,p4)*zb(p9,p10) )
c      mqbq = mqbq + mt2*denwp2**(-1) * ( za(p1,p7)*za(p2,p9)*zb(p2,p4)*
c     &    zb(p2,p10) + za(p1,p7)*za(p9,p10)*zb(p2,p10)*zb(p4,p10) )
c      write(6,*) 'mqbq',mqbq
c      mqqb=mqqb + denwp1**(-1) * (  - za(p1,p9)*za(p2,q4)*za(p7,a7)*zb(
c     &    p1,p10)*zb(p1,a7)*zb(p4,q4) + za(p2,q4)*za(p7,a7)*za(p9,p10)*
c     &    zb(p1,p10)*zb(p4,q4)*zb(p10,a7) )
c      mqqb = mqqb + denwp2**(-1) * ( za(p2,p9)*za(p2,q4)*za(p7,a7)*zb(
c     &    p1,a7)*zb(p2,p10)*zb(p4,q4) + za(p2,p9)*za(p7,a7)*za(p9,q4)*
c     &    zb(p1,a7)*zb(p4,q4)*zb(p9,p10) )
c      mqqb = mqqb + mt2*denwp1**(-1) * ( za(p1,p9)*za(p2,p7)*zb(p1,p4)*
c     &    zb(p1,p10) + za(p2,p7)*za(p9,p10)*zb(p1,p10)*zb(p4,p10) )
c      mqqb = mqqb + mt2*denwp2**(-1) * (  - za(p2,p7)*za(p2,p9)*zb(p1,
c     &    p4)*zb(p2,p10) + za(p2,p9)*za(p7,p9)*zb(p1,p4)*zb(p9,p10) )
c      write(6,*) 'mqqb',mqqb


      mqbq=-za(p1,p9)*za(a7,p7)*zb(a7,p2)*zb(p4,q4)/denwp1
     &  * (za(q4,p1)*zb(p1,p10)+za(q4,p9)*zb(p9,p10))
      mqbq = mqbq+za(p1,q4)*za(a7,p7)*zb(p4,q4)*zb(p10,p2)/denwp2
     &  *(za(p9,p2)*zb(p2,a7)+za(p9,p10)*zb(p10,a7))
      mqbq = mqbq-mt2*za(p1,p9)*zb(p4,p2)/denwp1
     &  *(za(p7,p1)*zb(p1,p10)+za(p7,p9)*zb(p9,p10))
      mqbq = mqbq+mt2*za(p1,p7)*zb(p10,p2)/denwp2
     &  *(za(p9,p2)*zb(p2,p4)+za(p9,p10)*zb(p10,p4))
c      write(6,*) 'mqbq',mqbq
     
      mqqb=-za(p2,p9)*za(a7,p7)*zb(a7,p1)*zb(p4,q4)/denwp2
     &  * (za(q4,p2)*zb(p2,p10)+za(q4,p9)*zb(p9,p10))
      mqqb = mqqb+za(p2,q4)*za(a7,p7)*zb(p4,q4)*zb(p10,p1)/denwp1
     &  *(za(p9,p1)*zb(p1,a7)+za(p9,p10)*zb(p10,a7))
      mqqb = mqqb-mt2*za(p2,p9)*zb(p4,p1)/denwp2
     &  *(za(p7,p2)*zb(p2,p10)+za(p7,p9)*zb(p9,p10))
      mqqb = mqqb+mt2*za(p2,p7)*zb(p10,p1)/denwp1
     &  *(za(p9,p1)*zb(p1,p4)+za(p9,p10)*zb(p10,p4))
c      write(6,*) 'mqqb',mqqb
c      pause
      wtqqb=aveqq*facqqb*cdabs(mqqb)**2
      wtqbq=aveqq*facqqb*cdabs(mqbq)**2
     
C----set all elements to zero
      do j=-nf,nf
      do k=-nf,nf
c--set msq=0 to initalize
      msq(j,k)=0d0
          if ((j .gt. 0) .and. (k .lt. 0)) then
            msq(j,k)=Vsq(j,k)*wtqqb
          elseif ((j .lt. 0) .and. (k .gt. 0)) then
            msq(j,k)=Vsq(j,k)*wtqbq
          endif
      enddo
      enddo
      return
      end
